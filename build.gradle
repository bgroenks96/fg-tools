plugins {
  id 'net.researchgate.release' version '2.3.4'
}

allprojects {
  apply plugin: "idea"
  apply plugin: "eclipse"
  apply plugin: "java"

  group = projectGroup
  version = projectVersion

  sourceCompatibility = projectSourceCompatibility
  targetCompatibility = projectTargetCompatibility

  repositories {
    mavenCentral()
  }
}

subprojects {
  apply plugin: "maven"

  tasks.withType (Jar) {
    archivesBaseName = "$rootProject.name-$project.name"
  }

  task sourcesJar (type: Jar, dependsOn: classes) {
    classifier = "sources"
    from sourceSets.main.allSource
  }

  task javadocJar (type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    javadoc.options.links ("http://docs.oracle.com/javase/7/docs/api/");
    from javadoc.destinationDir
  }

  artifacts {
    archives sourcesJar
    archives javadocJar
  }
}

project (":common") {
  dependencies {
    compile "com.google.guava:guava:18.0"
    compile "org.apache.commons:commons-lang3:3.3.2"
    compile "com.google.code.findbugs:jsr305:2.0.3"
    compile "org.slf4j:slf4j-api:1.7.6"
    testCompile "org.hamcrest:hamcrest-all:1.3"
    testCompile "junit:junit:4.11"
    testCompile "org.mockito:mockito-core:1.9.5"
    testRuntime "ch.qos.logback:logback-classic:1.1.2"
  }
}

project (":net") {
  dependencies {
    compile project (":common")
  }
}

if (JavaVersion.current().isJava8Compatible()) {
  allprojects {
    tasks.withType (Javadoc) {
      options.addStringOption ("Xdoclint:none", "-quiet")
    }
  }
}

idea {
  project {
    languageLevel = "${projectSourceCompatibility}"
  }

  module {
    downloadJavadoc = true
    downloadSources = true
  }
}

tasks.eclipse.doLast {
  delete ".project"
}
