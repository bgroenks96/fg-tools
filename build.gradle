allprojects {
  apply plugin: "idea"
  apply plugin: "eclipse"
  apply plugin: "maven"
  apply plugin: "java"

  group = projectGroup
  version = projectVersion

  sourceCompatibility = projectSourceCompatibility
  targetCompatibility = projectTargetCompatibility

  repositories {
    mavenCentral()
    maven { url "https://raw.github.com/synergian/wagon-git/releases" }
  }
}

subprojects {
  configurations {
    deployerJar
  }

  dependencies {
    deployerJar "ar.com.synergian:wagon-git:0.2.3"
  }

  task sourcesJar (type: Jar, dependsOn: classes) {
    classifier = "sources"
    from sourceSets.main.allSource
  }

  task javadocJar (type: Jar, dependsOn: javadoc) {
    classifier = "javadoc"
    from javadoc.destinationDir
    javadoc.options.links "http://docs.oracle.com/javase/7/docs/api/"
  }

  artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
  }

  uploadArchives {
    repositories {
      mavenDeployer {
        configuration = configurations.deployerJar
        uniqueVersion = false

        repository (url: "git:releases://git@github.com:forerunnergames/fg-tools.git") {
          authentication (userName: githubUsername, password: githubPassword)
        }

        snapshotRepository (url: "git:snapshots://git@github.com:forerunnergames/fg-tools.git") {
          authentication (userName: githubUsername, password: githubPassword)
        }

        pom.project {
          name "Forerunner Games Utility Library"
          description "Provides simple, re-usable game development tools."
          packaging "jar"
          url "https://github.com/forerunnergames/fg-tools"

          issueManagement {
            url "https://github.com/forerunnergames/fg-tools/issues"
            system "GitHub Issues"
          }

          scm {
            url "https://github.com/forerunnergames/fg-tools"
            connection "scm:git:git://github.com/forerunnergames/fg-tools.git"
            developerConnection "scm:git:git@github.com:forerunnergames/fg-tools.git"
          }

          developers {
            developer {
              id "3xp0n3nt"
              name "Aaron Mahan"
              email "aaron@forerunnergames.com"
              url "http://forerunnergames.com/"
            }
          }
        }
      }
    }
  }
}

project (":common") {
  dependencies {
    compile project (":randomx")
    compile "com.google.guava:guava:13.0.1"
    compile "com.google.code.findbugs:jsr305:2.0.3"
    compile "org.slf4j:slf4j-api:1.7.2"
    runtime "ch.qos.logback:logback-classic:1.0.13"
    testCompile "org.hamcrest:hamcrest-all:1.3"
    testCompile "junit:junit:4.11"
    testCompile "org.mockito:mockito-core:1.9.5"
  }
}

idea {
  project {
    languageLevel = "${projectSourceCompatibility}"
  }

  module {
    downloadJavadoc = true
    downloadSources = true
  }
}

tasks.eclipse.doLast {
  delete ".project"
}
