/*
 * Copyright © 2011 - 2013 Aaron Mahan
 * Copyright © 2013 - 2016 Forerunner Games, LLC
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

plugins {
  id "com.jfrog.artifactory" version "4.4.9"
  id "com.jfrog.bintray" version "1.7.3"
  id "net.researchgate.release" version "2.4.1"
  id "com.github.ben-manes.versions" version "0.13.0"
  id "com.github.kt3k.coveralls" version "2.7.1"
}

allprojects {
  repositories {
    jcenter ()
    mavenCentral ()
  }

  apply plugin: "com.jfrog.artifactory"
  apply plugin: "com.github.kt3k.coveralls"
  apply plugin: "jacoco"
  apply plugin: "java"
  apply plugin: "idea"
  apply plugin: "eclipse"

  [compileJava, compileTestJava, javadoc]*.options*.encoding = "UTF-8"

  sourceCompatibility = projectSourceCompatibility
  targetCompatibility = projectTargetCompatibility

  status = version.endsWith ("-SNAPSHOT") ? "integration" : "release"
}

rootProject.jar.enabled = false
rootProject.artifactoryPublish.skip = true
rootProject.bintrayUpload.enabled = false

subprojects {
  apply plugin: "maven"
  apply plugin: "com.jfrog.bintray"

  configurations {
    published
  }

  manifest {
    attributes "provider": "gradle"
  }

  tasks.withType (Jar) {
    archivesBaseName = "$rootProject.name-$project.name"
  }

  task sourceJar (type: Jar) {
    classifier = "sources"
    from sourceSets.main.allSource
  }

  task javadocJar (type: Jar, dependsOn: javadoc) {
    classifier = "javadoc"
    javadoc.options.links ("http://docs.oracle.com/javase/7/docs/api/");
    from javadoc.destinationDir
  }

  artifactoryPublish {
    dependsOn sourceJar, javadocJar
  }

  bintrayUpload {
    dependsOn sourceJar, javadocJar
  }

  artifacts {
    published sourceJar
    published javadocJar
    archives sourceJar
    archives javadocJar
  }

  test {
    beforeTest { desc ->
      logger.quiet ("Running test: " + desc)
    }

    afterTest { desc, result ->
      if (result.resultType == TestResult.ResultType.FAILURE) {
        logger.quiet ("Failed test ${desc.name} [${desc.className}] with exception: ${result.exception}")
        if (result.exception != null) {
          result.exception.printStackTrace ()
        }
      }
    }

    testLogging.showStandardStreams = true;
  }

  bintray {
    user = project.hasProperty ("bintrayUser") ? bintrayUser : System.getenv ("BINTRAY_USER")
    key = project.hasProperty ("bintrayApiKey") ? bintrayApiKey : System.getenv ("BINTRAY_API_KEY")
    configurations = ["published", "archives"]
    dryRun = false
    publish = true
    pkg {
      repo = "repository"
      name = "fg-tools"
      desc = "Simple utility library for Java game development"
      userOrg = "forerunnergames"
      websiteUrl = "https://github.com/forerunnergames/fg-tools"
      issueTrackerUrl = "https://github.com/forerunnergames/fg-tools/issues"
      vcsUrl = "https://github.com/forerunnergames/fg-tools.git"
      licenses = ["MIT"]
      publicDownloadNumbers = true
    }
  }
}

configurations {
  published
}

artifactory {
  contextUrl = "http://oss.jfrog.org/artifactory"
  publish {
    repository {
      repoKey = "oss-snapshot-local"
      username = project.hasProperty ("bintrayUser") ? bintrayUser : System.getenv ("BINTRAY_USER")
      password = project.hasProperty ("bintrayApiKey") ? bintrayApiKey : System.getenv ("BINTRAY_API_KEY")
      maven = true
    }
    defaults {
      publishConfigs ("archives", "published")
      publishPom = true
      publishIvy = false
    }
  }
  clientConfig.includeEnvVars = true
  if (System.getenv ("TRAVIS")) {
    clientConfig.info.buildNumber = System.getenv ("TRAVIS_BUILD_NUMBER")
    clientConfig.info.vcsRevision = System.getenv ("TRAVIS_COMMIT")
  }
  resolve {
    repository {
      repoKey = "jcenter"
      username = hasProperty ("bintrayUser") ? bintrayUser : System.getenv ("BINTRAY_USER")
      password = hasProperty ("bintrayApiKey") ? bintrayApiKey : System.getenv ("BINTRAY_API_KEY")
      maven = true
    }
  }
}

release {
  failOnCommitNeeded = true
  failOnPublishNeeded = true
  failOnSnapshotDependencies = true
  failOnUnversionedFiles = true
  failOnUpdateNeeded = true
  revertOnFail = true
  git {
    requireBranch = "master"
    pushToRemote = "origin"
  }
}

project (":common") {
  dependencies {
    compile "com.google.guava:guava:20.0"
    compile "org.apache.commons:commons-lang3:3.5"
    compile "com.google.code.findbugs:jsr305:3.0.1"
    compile "org.slf4j:slf4j-api:1.7.21"
    testCompile "org.hamcrest:hamcrest-all:1.3"
    testCompile "junit:junit:4.12"
    testCompile "org.mockito:mockito-core:2.2.22"
    testRuntime "ch.qos.logback:logback-classic:1.1.7"
  }
}

project (":net") {
  dependencies {
    compile project (":common")
    testCompile "org.hamcrest:hamcrest-all:1.3"
    testCompile "junit:junit:4.12"
    testCompile "org.mockito:mockito-core:2.2.22"
    testRuntime "ch.qos.logback:logback-classic:1.1.7"
  }
}

task jacocoRootReport (type: JacocoReport, group: "Coverage reports") {
  description = "Generates an aggregate report from all subprojects"
  dependsOn (subprojects.test)

  additionalSourceDirs = files (subprojects.sourceSets.main.allSource.srcDirs)
  sourceDirectories = files (subprojects.sourceSets.main.allSource.srcDirs)
  classDirectories = files (subprojects.sourceSets.main.output)
  executionData = files (subprojects.jacocoTestReport.executionData)

  reports {
    html.enabled = true // human readable
    xml.enabled = true // required by coveralls
  }

  doFirst {
    executionData = files (executionData.findAll { it.exists () })
  }
}

coveralls {
  sourceDirs = subprojects.sourceSets.main.allSource.srcDirs.flatten ()
  jacocoReportPath = "${buildDir}/reports/jacoco/jacocoRootReport/jacocoRootReport.xml"
}

tasks.coveralls {
  group = "Coverage reports"
  description = "Uploads the aggregated coverage report to Coveralls"
  dependsOn jacocoRootReport
  onlyIf { System.env."CI" }
}

task wrapper (type: Wrapper) {
  gradleVersion = "2.10"
}

tasks.artifactoryPublish.doFirst {
  assert project.status == "integration": "Only a snapshot version (not ${project.version}) can be published to Artifactory."
}

tasks.bintrayUpload.doFirst {
  assert project.status == "release": "Only a release version (not ${project.version}) can be published to Bintray."
}

if (JavaVersion.current ().isJava8Compatible ()) {
  allprojects {
    tasks.withType (Javadoc) {
      options.addStringOption ("Xdoclint:none", "-quiet")
    }
  }
}

idea {
  project {
    languageLevel = "${projectSourceCompatibility}"
  }

  module {
    downloadJavadoc = true
    downloadSources = true
  }
}

tasks.eclipse.doLast {
  delete ".project"
}
